-------------------------------------------------------------------------

-------------------------------------------------------------------------

-------------------------------------------------------------------------

-------------------------------------------------------------------------
Sub ボタン④新規シート作成と既存シートのデータをコピーペーストするプログラム()


    Set ws = Worksheets("2操作画面（編集厳禁）")

    Dim 名前 As String: 名前 = ws.Range("B2") 'Worksheets("2操作画面（編集厳禁）")から生徒名をコピー
    Dim シート検索 As Worksheet
    
    If 名前 = "" Then 'もし生徒名が入力されていない場合、エラーコードを表示
        MsgBox "生徒名を入力してください。"
        Exit Sub
    End If 'エラーがなければ、以下のプログラムを実行
    
    For Each シート検索 In Sheets                               'シートの中から同じ生徒名のシートがないかFor Eachループで探す
        If シート検索.Name = 名前 Then
            MsgBox "同じ生徒の希望日程表を二度作成しています。" 'あればエラーメッセージを表示
            Exit Sub
        End If
    Next シート検索 'エラーがなければ、以下のプログラムを実行
    
    ws.Copy after:=ws         'Worksheets("2操作画面（編集厳禁）")を複製
    ActiveSheet.Name = "仮名" '複製したシートに仮名をつける
    
    '新しい表の調整
    With Worksheets("仮名")                                  '余分な部分の削除
        .Rows("3:14").Delete                                 'シート"仮名"の3行目から14行目を削除（VBA画面でのみ変更可能）
        .Range("P1:AC2").Delete                              'P1:からC2セルのメモ欄を削除'（VBA画面でのみ変更可能）
    End With
    
    Dim 余白上 As Double: 余白上 = ws.Range("Q3")      '上の余白はQ3セルを参照
    Dim 余白下 As Double: 余白下 = ws.Range("R3")      '下の余白はR3セルを参照
    Dim 余白右 As Double: 余白右 = ws.Range("S3")      '左の余白はS3セルを参照
    Dim 余白左 As Double: 余白左 = ws.Range("T3")      '右の余白はT3セルを参照
    Dim 行高 As Double: 行高 = ws.Range("U3")          '行の高さはU3セルを参照
    Dim 列幅 As Double: 列幅 = ws.Range("V3")          '列の幅はV3セルを参照
    Dim フォント As String: フォント = ws.Range("W3")  '文字書式はW3セルを参照
    Dim 文字大 As Double: 文字大 = ws.Range("X3")      '文字サイズはX3セルを参照

    Call ページレイアウトと文字書式(余白上, 余白下, 余白右, 余白左, 行高, 文字大, フォント)

    '条件付き書式の設定
    '---各教科で使用する色とその濃度を指定（以下全てVBA画面でのみ変更可能）---
    Dim 季英 As Double: 季英 = 10
    Dim 季英濃度  As Double: 季英濃度 = 0.6
    
    Dim 季国 As Double: 季国 = 9           '色は色一覧の左から1,2,3...,9,10となる。
    Dim 季国濃度 As Double: 季国濃度 = 0.6 '濃度は基本色 +QQ％を小数表記したもの
    
    Dim 季数 As Double: 季数 = 8
    Dim 季数濃度 As Double: 季数濃度 = 0.6
    
    Dim 季社 As Double: 季社 = 6
    Dim 季社濃度  As Double: 季社濃度 = 0.6
    
    Dim 季理 As Double: 季理 = 5
    Dim 季理濃度 As Double: 季理濃度 = 0.6
    '---（ここまで全てVBA画面でのみ変更可能）---
    
    Call 条件付き書式(季英, 季英濃度, 季国, 季国濃度, 季数, 季数濃度, 季社, 季社濃度, 季理, 季理濃度)

    
    '複製したシートの名前を生徒名に変更
    Worksheets("仮名").Name = 名前
    
    '初期化できるようにN1セルの判定マーカーを削除
    ws.Range("N1").ClearContents
    
    'ファイルを保存
    ActiveWorkbook.Save
    
    
End Sub

Function ページレイアウトと文字書式(ByVal ぺ余白上 As Double, ByVal ぺ余白下 As Double, ByVal ぺ余白右 As Double, ByVal ぺ余白左 As Double, ByVal ぺ行高 As Double, ByVal ぺ文字大 As Double, ByVal ぺフォント As String)


    'ページレイアウト
    With Worksheets("仮名").PageSetup
        .TopMargin = Application.CentimetersToPoints(ぺ余白上)    '上の余白を変更 (Application.CentimetersToPointsはcmを1ポイント=約0.35mmに変換するメソッド)
        .BottomMargin = Application.CentimetersToPoints(ぺ余白下) '下の余白を変更
        .LeftMargin = Application.CentimetersToPoints(ぺ余白右)   '左の余白を変更
        .RightMargin = Application.CentimetersToPoints(ぺ余白左)  '右の余白を変更
        .HeaderMargin = 0                                         'ヘッダーを0にする
        .FooterMargin = 0                                         'フッターを0にする
        .CenterVertically = True                                  '印刷位置を垂直方向のページ中央にする
        .CenterHorizontally = True                                '印刷位置を水平方向のページ中央にする
    End With
    
    '文字書式
    With Worksheets("仮名").Cells
        .RowHeight = Application.CentimetersToPoints(ぺ行高) '行の高さを変更
        .Font.Name = ぺフォント                              'フォントを変更
        .Font.Size = ぺ文字大                                '文字サイズを変更
        .VerticalAlignment = xlTop                           '垂直方向の文字配置を上揃えにする
        .HorizontalAlignment = xlCenter                      '水平方向の文字配置を中央揃えにする
    End With


End Function

Function 条件付き書式(ByVal 色一 As Double, ByVal 色濃一 As Double, ByVal 色二 As Double, ByVal 色濃二 As Double, ByVal 色三 As Double, ByVal 色濃三 As Double, ByVal 色四 As Double, ByVal 色濃四 As Double, ByVal 色五 As Double, ByVal 色濃五 As Double)

    Set ws = Worksheets("2操作画面（編集厳禁）")

    Dim 行終 As Integer: 行終 = Range("A3").End(xlDown).Row       'セルの範囲を測定して結果を変数に格納(行)（VBA画面でのみ変更可能）
    Dim 列終 As Integer: 列終 = Range("A3").End(xlToRight).Column 'セルの範囲を測定して結果を変数に格納(列)（VBA画面でのみ変更可能）
    Dim 書式設定一  As FormatCondition
    Dim 書式設定二  As FormatCondition
    Dim 書式設定三  As FormatCondition
    Dim 書式設定四  As FormatCondition
    Dim 書式設定五  As FormatCondition
    
    
    With Worksheets("仮名").Range(Cells(1, 1), Cells(行終, 列終)) '教科別の書式設定
        .FormatConditions.Delete                                  '書式設定を初期
        Set 書式設定一 = .FormatConditions.Add(Type:=xlCellValue, Operator:=xlEqual, Formula1:=ws.Range("Q11").Value) 'セルの値(Formula1:=Range("Q11"))を参照して(Type:=xlCellValue、例:季　英語)と完全一致(Operator:=xlEqual)しているセルに書式を適用
        With 書式設定一.Interior
            .ThemeColor = 色一       'ThemeColorは色一覧の列
            .TintAndShade = 色濃一 'TintAndShadeは基本色 +QQ%の場所（％を小数表記）
        End With
        Set 書式設定二 = .FormatConditions.Add(Type:=xlCellValue, Operator:=xlEqual, Formula1:=ws.Range("R11").Value)
        With 書式設定二.Interior
            .ThemeColor = 色二
            .TintAndShade = 色濃二
        End With
        Set 書式設定三 = .FormatConditions.Add(Type:=xlCellValue, Operator:=xlEqual, Formula1:=ws.Range("S11").Value)
        With 書式設定三.Interior
            .ThemeColor = 色三
            .TintAndShade = 色濃三
        End With
        Set 書式設定四 = .FormatConditions.Add(Type:=xlCellValue, Operator:=xlEqual, Formula1:=ws.Range("T11").Value)
        With 書式設定四.Interior
            .ThemeColor = 色四
            .TintAndShade = 色濃四
        End With
        Set 書式設定五 = .FormatConditions.Add(Type:=xlCellValue, Operator:=xlEqual, Formula1:=ws.Range("U11").Value)
        With 書式設定五.Interior
            .ThemeColor = 色五
            .TintAndShade = 色濃五
        End With
    End With


End Function
-------------------------------------------------------------------------

-------------------------------------------------------------------------
Sub ボタン⑦表の設定をすべてデフォルトにする()


    Set ws = Worksheets("2操作画面（編集厳禁）")
    
    '余白の上下左右、行の高さ、列の幅、フォント、字大きさの初期化
    ws.Range("Q3") = 2.5         '余白の上
    ws.Range("R3") = 2.5         '余白の下
    ws.Range("S3") = 2           '余白の右
    ws.Range("T3") = 2           '余白の左
    ws.Range("U3") = 0.48        '行の高さ
    ws.Range("V3") = 1.7         '列の幅
    ws.Range("W3") = "Yu Gothic" 'フォント
    ws.Range("X3") = 10          '字大きさ
    
    
    '列に入力する日数、一日のコマ数、見出しの束数の初期化
    ws.Range("Q7") = 14 '列に入力する日数
    ws.Range("U7") = 9  '一日のコマ数
    ws.Range("Y7") = 3  '見出しの束数の初期化
    
    '教科名の初期化
    ws.Range("Q11:AB11").ClearContents
    
    Dim 教科名 As Variant: 教科名 = Array("季　英語", "季　国語", "季　数学", "季　社会", "季　理科", "英語", "国語", "数学", "社会", "理科")
    
    ws.Range("Q11:Z11") = 教科名
    
    '各コマの開始時間の初期化
    ws.Range("Q9:AP9").ClearContents
    
    Dim 時刻 As Variant: 時刻 = Array("日/時", "13:00", "14:00", "15:00", "16:00", "17:00", "18:00", "19:00", "20:00", "21:00")
    
    ws.Range("Q9:Z9") = 時刻


End Sub
-------------------------------------------------------------------------
